repositories{
    mavenCentral()
    maven{
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.moowork.gradle:gradle-node-plugin:0.12'
    classpath 'com.moowork.gradle:gradle-gulp-plugin:0.12'
    classpath 'org.codehaus.groovy:groovy-backports-compat23:2.3.5'
  }
}

apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'com.moowork.node'
apply plugin: 'com.moowork.gulp'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile 'javax.ws.rs:jsr311-api:1.1.1'
    compile 'com.google.code.gson:gson:2.6.2'
    compile group: 'org.apache.solr', name: 'solr-solrj', version: '5.4.1'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.3.1'
    compile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.3.1'
    compile group: 'com.squareup.okhttp3', name: 'okhttp-ws', version: '3.3.1'
    compile group: 'com.squareup.okhttp3', name: 'okhttp-urlconnection', version: '3.3.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.3.6'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.3.2'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5'
    compile group: 'commons-validator', name: 'commons-validator', version: '1.4.0'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.5'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2'
    compile group: 'org.jsoup', name: 'jsoup', version: '1.9.2'
    compile group: 'com.ibm.watson.developer_cloud', name: 'java-sdk', version:'3.0.2-SNAPSHOT'
    compile group: 'com.cloudant', name: 'cloudant-client', version: '2.5.0'

    testCompile 'com.sun.jersey:jersey-client:1.19.1'
    testCompile 'org.mockito:mockito-core:1.+'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'
    testCompile group: 'com.squareup.okhttp3', name: 'mockwebserver', version:'3.3.1'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.7'
    testCompile group: 'com.google.guava', name: 'guava', version:'19.0'
    testCompile group: 'junit', name: 'junit-dep', version:'4.11'
    testCompile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.5'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task cleanWar(type: Delete) {
    delete "build/libs"
}

artifacts {
    archives sourcesJar
}

war {
    archiveName = 'conversation-enhanced-0.1-SNAPSHOT.war'
}

gulp_build.dependsOn 'installGulp'
gulp_build.dependsOn 'npmInstall'
gulp_build.dependsOn 'cleanWar'
war.mustRunAfter 'gulp_build'
defaultTasks 'build'

build.dependsOn gulp_build
node {
    // Version of node to use.
    version = '4.4.7'

    // Enabled the automatic download.
    download = true
}
gulp {
    // Set the directory where gulpfile.js should be found
    workDir = file("${project.projectDir}")

    // Whether colors should output on the terminal
    colors = true

    // Whether output from Gulp should be buffered - useful when running tasks in parallel
    bufferOutput = false
}
